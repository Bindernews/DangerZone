buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

def username = "DangerZone"

version = "${minecraft_version}-${mod_version}"
group = "com.vortexel.dangerzone"
archivesBaseName = "dangerzone"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    clientRunArgs += "--username=${username}"

    // This replaces
    replace "@VERSION@", project.version
    replace "@MOD_ID@", project.mod_id
    replace "@MOD_NAME@", project.mod_name
    replaceIn "DangerZone.java"
    replaceIn "com/vortexel/dangerzone/api/package-info.java"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = mcp_mapping
}

dependencies {
    compile "org.projectlombok:lombok:1.18.6"
//    annotationProcessor "org.projectlombok:lombok:1.18.6"

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "mod_id", project.mod_id
    inputs.property "mod_name", project.mod_name

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version,
                'mod_id':project.mod_id, 'mod_name':project.mod_name
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    from sourceSets.main.output
    from sourceSets.api.output
}

task apiJar(type: Jar) {
    classifier = 'api'
    from sourceSets.main.output     // save .class files
    from sourceSets.main.allJava    // save .java files
    from sourceSets.api.allJava
    include 'com/vortexel/dangerzone/api/**/*'  // Only include API and subdirectories
}

artifacts {
    archives apiJar
}
